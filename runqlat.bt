#!/usr/bin/bpftrace
/*
 * runqlat.bt	CPU scheduler run queue latency as a histogram.
 *		For Linux, uses bpftrace, eBPF.
 *
 * This is a bpftrace version of the bcc tool of the same name.
 *
 * Copyright 2018 Netflix, Inc.
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 * 17-Sep-2018	Brendan Gregg	Created this.
 */

#include <linux/sched.h>

BEGIN
{
	printf("Tracing CPU scheduler... Hit Ctrl-C to end.\n");
}

tracepoint:sched:sched_wakeup,
tracepoint:sched:sched_wakeup_new
/ cpu <= 112 && cpu < 128/
{
	@qtime[args->pid] = nsecs;
}

tracepoint:sched:sched_switch
/ cpu <= 112 && cpu < 128/
{
	if (args->prev_state == TASK_RUNNING) {
		@qtime[args->prev_pid] = nsecs;
	}

	$ns = @qtime[args->next_pid];
	if ($ns) {
        $usec = (nsecs - $ns) / 1000;
		@usecs = hist($usec);
        @usec_avg = stats($usec);
	}
	delete(@qtime[args->next_pid]);
}

END
{
	clear(@qtime);
}
